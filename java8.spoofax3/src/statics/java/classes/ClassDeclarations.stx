module statics/java/classes/ClassDeclarations

imports

  signatures/syntax/java/classes/ClassDeclarations-sig

  statics/java/JRE
  statics/java/classes/Main
  statics/java/classes/ConstructorDeclarations
  statics/java/classes/EnumDeclarations
  statics/java/classes/FieldDeclarations
  statics/java/classes/InstanceInitializers
  statics/java/classes/MethodDeclarations
  statics/java/classes/StaticInitializers
  statics/java/interfaces/Annotations
  statics/java/interfaces/InterfaceDeclarations
  statics/java/names/Main
  statics/java/names/TypeNames
  statics/java/types/Main
  statics/java/types/ReferenceTypes
  statics/java/types/TypeVariable
  statics/java/util/Boolean


rules // 8.1. Class Declarations

  clsDeclOk : scope * ClassDeclaration * scope * scope

  clsDeclOk(s, NormalClassDeclaration2ClassDeclaration(normalClsDecl), s_def, s_staticDef) :-
    normalClsDeclOk(s, normalClsDecl, s_def, s_staticDef),
    true.

  clsDeclOk(s, EnumDeclaration2ClassDeclaration(enumDecl), s_def, s_staticDef) :-
    enumDeclOk(s, enumDecl, s_staticDef),
    true.


  normalClsDeclOk : scope * NormalClassDeclaration * scope * scope

  normalClsDeclOk(s, ClassDeclaration(clsModList, id, typeParamsOpt, superClsOpt, superIntfsOpt, clsBodyDeclList), s_def, s_staticDef) :-
  {isStatic s_cls s_typeParams s_clsStatic}
    clsModListOk(s, clsModList),
    isStatic == hasStaticClsMod(clsModList),

    new s_cls,
    addLexical(s_cls, isStatic, s),

    declareType2(s_def, s_staticDef, isStatic, id, s_cls, CLASS()),
    // FIXME noDuplicateType
    // FIXME noEnclosingTypeSameName
    // FIXME noClassCycle

    new s_typeParams,
    addLexical(s_typeParams, isStatic, s),
    s_cls -TYPE_PARAMS-> s_typeParams,
    typeParamsOptOk(s_typeParams, typeParamsOpt),

    superClsOptOk(s_typeParams, superClsOpt, s_cls),
    superIntfsOptOk(s_typeParams, superIntfsOpt, s_cls),

    new s_clsStatic,
    s_cls -STATIC_MEMBERS-> s_clsStatic,

    declareImplicitCtorIfMissing(s_cls, clsBodyDeclList),

    clsBodyDeclListOk(s_cls, clsBodyDeclList, s_cls, s_clsStatic),
    true.


  declareType2 : scope * scope * B * Id * scope * KIND

  declareType2(s_def, s_staticDef, F(), id, s_ty, K) :- declareType(s_def, id, s_ty, K).
  declareType2(s_def, s_staticDef, T(), id, s_ty, K) :- declareType(s_staticDef, id, s_ty, K).


  clsModOk : scope * ClassModifier
  clsModListOk maps clsModOk(*, list(*))

  clsModOk(s, Annotation2ClassModifier(anno)) :-
    annoOk(s, anno),
    true.
  clsModOk(s, Public2ClassModifier(_)) :- true.
  clsModOk(s, Protected2ClassModifier(_)) :- true.
  clsModOk(s, Private2ClassModifier(_)) :- true.
  clsModOk(s, Abstract2ClassModifier(_)) :- true.
  clsModOk(s, Static2ClassModifier(_)) :- true.
  clsModOk(s, Final2ClassModifier(_)) :- true.
  clsModOk(s, Strictfp2ClassModifier(_)) :- true.


  hasStaticClsMod : list(ClassModifier) -> B

  hasStaticClsMod([Static2ClassModifier(_)|_]) = T().
  hasStaticClsMod([_|clsModList]) = hasStaticClsMod(clsModList).
  hasStaticClsMod([]) = F().


  typeParamsOptOk : scope * TypeParameters-OPT

  typeParamsOptOk(s, NoTypeParameters()).

  typeParamsOptOk(s, SomeTypeParameters(typeParams)) :- typeParamsOk(s, typeParams).


  typeParamsOk : scope * TypeParameters

  typeParamsOk(s, TypeParameters(typeParamList)) :-
    typeParamListOk(s, typeParamList).


  superClsOptOk : scope * SuperClass-OPT * scope

  superClsOptOk(s, NoSuperClass(), s_cls) :-
    setSuperCls(s_cls, objectOk(s)),
    true.

  superClsOptOk(s, SomeSuperClass(SuperClass(clsType)), s_cls) :-
  {T}
    T == clsTypeOk(s, clsType),
    setSuperCls(s_cls, T).


  setSuperCls : scope * TYPE

  setSuperCls(s_cls, T) :-
    isClsType(T),
    s_cls -EXTENDS-> typeScope(T).

  superIntfsOptOk : scope * SuperInterfaces-OPT * scope
  superIntfsOptOk(s, NoSuperInterfaces(), s_cls).
  superIntfsOptOk(s, SomeSuperInterfaces(superInterfaces), s_cls) :- superIntfsOk(s, superInterfaces, s_cls).

  superIntfsOk : scope * SuperInterfaces * scope
  superIntfsOk(s, SuperInterface(clsTypeList), s_cls) :-
  {Ts}
    Ts == clsTypeListOk(s, clsTypeList),
    addSuperIntfList(s_cls, Ts).

  addSuperIntf : scope * TYPE
  addSuperIntfList maps addSuperIntf(*, list(*))

  addSuperIntf(s_cls, T) :-
    isIntfType(T),
    s_cls -IMPLEMENTS-> typeScope(T).


  declareImplicitCtorIfMissing : scope * list(ClassBodyDeclaration)

  declareImplicitCtorIfMissing(s, [ConstructorDeclaration2ClassBodyDeclaration(_)|_]).

  declareImplicitCtorIfMissing(s, [_|clsBodyDecls]) :-
    declareImplicitCtorIfMissing(s, clsBodyDecls).

  declareImplicitCtorIfMissing(s, []) :-
    declareImplicitCtor(s).


  clsBodyDeclOk : scope * ClassBodyDeclaration * scope * scope
  clsBodyDeclListOk maps clsBodyDeclOk(*, list(*), *, *)

  clsBodyDeclOk(s, ClassMemberDeclaration2ClassBodyDeclaration(clsMemDecl), s_def, s_staticDef) :-
    clsMemDeclOk(s, clsMemDecl, s_def, s_staticDef),
    true.

  clsBodyDeclOk(s, InstanceInitializer2ClassBodyDeclaration(instInit), s_def, s_staticDef) :-
    instInitOk(s, instInit),
    true.

  clsBodyDeclOk(s, StaticInitializer2ClassBodyDeclaration(staticInit), s_def, s_staticDef) :-
    staticInitOk(s, staticInit),
    true.

  clsBodyDeclOk(s, ConstructorDeclaration2ClassBodyDeclaration(ctorDecl), s_def, s_staticDef) :-
    ctorDeclOk(s, ctorDecl, s_def),
    true.


  clsMemDeclOk : scope * ClassMemberDeclaration * scope * scope

  clsMemDeclOk(s, FieldDeclaration2ClassMemberDeclaration(fldDecl), s_def, s_staticDef) :-
    fldDeclOk(s, fldDecl, s_def, s_staticDef),
    true.

  clsMemDeclOk(s, MethodDeclaration2ClassMemberDeclaration(mthdDecl), s_def, s_staticDef) :-
    mthdDeclOk(s, mthdDecl, s_def, s_staticDef),
    true.

  clsMemDeclOk(s, ClassDeclaration2ClassMemberDeclaration(clsDecl), s_def, s_staticDef) :-
    clsDeclOk(s, clsDecl, s_def, s_staticDef),
    true.

  clsMemDeclOk(s, InterfaceDeclaration2ClassMemberDeclaration(intfDecl), s_def, s_staticDef) :-
    intfDeclOk(s, intfDecl, s_def, s_staticDef),
    true.

  clsMemDeclOk(s, SemiColon2ClassMemberDeclaration(semiColon), s_def, s_staticDef) :-
    true.


