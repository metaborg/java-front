module statics/java/interfaces/ConstantDeclarations

imports

  signatures/syntax/java/interfaces/ConstantDeclarations-sig

  statics/java/classes/FieldDeclarations
  statics/java/interfaces/Annotations
  statics/java/interfaces/Main
  statics/java/names/Main


rules // 9.3. Field (Constant) Declarations

  constDeclOk : scope * ConstantDeclaration * scope

  constDeclOk(s, ConstantDecl(constModList, unannType, varDeclList), s_staticDef) :-
  {s' T}
    constModListOk(s, constModList), // FIXME implicitly `public static final`

    new s',
    s' -STATIC_LEX-> s,

    T == unannTypeOk(s', unannType),
    varDeclListOk(s', T, varDeclList, s_staticDef).


  constModOk : scope * ConstantModifier
  constModListOk maps constModOk(*, list(*))

  constModOk(s, Annotation2ConstantModifier(anno)) :-
    annoOk(s, anno),
    true.
  constModOk(s, Public2ConstantModifier(_)) :- true.
  constModOk(s, Static2ConstantModifier(_)) :- true.
  constModOk(s, Final2ConstantModifier(_)) :- true.


