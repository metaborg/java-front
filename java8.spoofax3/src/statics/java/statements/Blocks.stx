module statics/java/statements/Blocks

imports

  signatures/syntax/java/statements/Blocks-sig

  statics/java/classes/ClassDeclarations
  statics/java/names/Main
  statics/java/statements/LocalVariableDeclarations
  statics/java/statements/Main
  statics/java/statements/Statements


rules // 14.2. Blocks

  blkOk : scope * Block

  blkOk(s, Block(blkStmtList)) :-
  {s_blk}
    new s_blk, s_blk -LEX-> s,
    blkStmtListOk(s_blk, blkStmtList).


  blkStmtListOk : scope * list(BlockStatement)

  blkStmtListOk(s, []).

  blkStmtListOk(s, [blkStmt|blkStmtList]) :-
  {s'}
    s' == blkStmtOk(s, blkStmt),
    blkStmtListOk(s', blkStmtList),
    true.


  blkStmtOk : scope * BlockStatement -> scope

  blkStmtOk(s, LocalVariableDeclarationStatement2BlockStatement(localVarDeclStmt)) = s' :-
    new s', s' -LEX-> s,
    localVarDeclStmtOk(s', localVarDeclStmt).

  blkStmtOk(s, ClassDeclaration2BlockStatement(clsDecl)) = s' :-
    new s', s' -LEX-> s,
    // FIXME clsDecl cannot be public, protected, private, or static
    clsDeclOk(s', clsDecl, s', s').

  blkStmtOk(s, Statement2BlockStatement(stmt)) = s :-
    stmtOk(s, stmt).

