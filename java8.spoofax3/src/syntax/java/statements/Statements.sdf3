module syntax/java/statements/Statements

// 14.5. Statements

imports
  syntax/java/classes/FieldDeclarations
  syntax/java/classes/MethodDeclarations
  syntax/java/lexical/Identifiers
  syntax/java/statements/Blocks
  syntax/java/names/Names
  syntax/java/types/ReferenceTypes
  syntax/java/expressions/Main

context-free sorts

  Statement
  ForInit
  ForUpdate
  Exprs
  SwitchGroup
  SwitchLabel
  CatchClause
  CatchFormalParameter
  CatchType
  CatchTypeRest
  Resource
    
context-free priorities

  Statement.IfElse  > Statement.If

context-free syntax

  Statement.Labeled = <<Id> : <Statement>>

  Statement.If      = <if(<Expression>) <Statement>>
  Statement.IfElse  = <if(<Expression>) <Statement> else <Statement>>
  
  Statement.While = <while(<Expression>) <Statement>>
  
  Statement.For     = <for(<ForInit>; <Expression?>; <ForUpdate>) <Statement>>
  Statement.ForEach = <for(<{VariableModifier " "}*> <UnannType> <VarDeclId> : <Expression>) <Statement>>
  
  ForInit = Exprs
  ForInit.VarDecls = <<{VariableModifier " "}*> <UnannType> <{VarDecl ", "}+>>
  
  ForUpdate = Exprs
  
  Exprs.ExprList =  <<{Expression ", "}*>>
  
  Statement = Block
  
  Statement.Empty = ";"
  
  Statement.ExpressionStatement = <<Expression>;>
  
  Statement = ExpressionName "<" ExpressionName ">" Expression ";" {reject}
  Statement = ExpressionName "<" ExpressionName "<" ExpressionName ">>" Expression ";" {reject} 
  Statement = ExpressionName "<" ExpressionName "<" ExpressionName "<" ExpressionName ">>>" Expression ";" {reject} 
  
  Statement.Assert = <assert <Expression>;>
  Statement.Assert = <assert <Expression> : <Expression>;>
  
  Statement.Switch = <
  switch(<Expression>) {
    <{SwitchGroup "\n"}*>
    <{SwitchLabel "\n"}*>
  }>
  
  SwitchGroup.SwitchGroup = <
  <{SwitchLabel "\n"}+>
  <{BlockStatement "\n"}+>
  >
  
  SwitchLabel.CaseExpr     = <case <Expression> :>
//  SwitchLabel.CaseEnumName = <case <Id> :>
  SwitchLabel.DefaultCase  = <default :>
  
  Statement.DoWhile = <
  do <Statement>
  while(<Expression>);>
  
  Statement.Break = <break;>
  Statement.Break = <break <Id>;>
  
  Statement.Continue = <continue;>
  Statement.Continue = <continue <Id>;>
  
  Statement.Return = <return;>
  Statement.Return = <return <Expression>;>
  
  Statement.Synchronized = <synchronized(<Expression>) <Block>>
  
  Statement.Throw = <throw <Expression>;>
  
  Statement.Try = <
  try <Block>
  <{CatchClause "\n"}+>>
  
  Statement.TryFinally = <
  try <Block>
  <{CatchClause "\n"}*>
  finally <Block>>
  
  CatchClause.Catch = <catch(<CatchFormalParameter>) <Block>>
  CatchFormalParameter.CatchParam = <<{VariableModifier " "}*> <CatchType> <VarDeclId>>
  CatchType.CatchType = <<UnannClassType> <{CatchTypeRest " "}*>>
  CatchTypeRest.CatchTypeElem = <| <ClassType>>
  
  Statement.TryWithResources = <
  try (<{Resource "; "}*>) <Block> 
  <{CatchClause "\n"}*>>
  
  Statement.TryWithResourcesSemiColon = <
  try (<{Resource "; "}*>;) <Block> 
  <{CatchClause "\n"}*>>
  
  Statement.TryWithResources = <
  try (<{Resource "; "}*>) <Block> 
  <{CatchClause "\n"}*>
  finally <Block>>
  
  Statement.TryWithResourcesSemiColon = <
  try (<{Resource "; "}*>;) <Block> 
  <{CatchClause "\n"}*>
  finally <Block>>
  
  Resource.Resource = <<{VariableModifier " "}*> <UnannType> <VarDeclId> = <Expression>>
    
template options
  tokenize : "(;"
    
// new SDF3 table generator
context-free priorities

  Statement.IfElse  > Statement.If