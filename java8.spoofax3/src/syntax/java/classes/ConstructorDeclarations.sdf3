module syntax/java/classes/ConstructorDeclarations

// 8.8. Constructor Declarations

imports
  syntax/java/classes/ClassDeclarations
  syntax/java/lexical/Identifiers
  syntax/java/lexical/Modifiers
  syntax/java/classes/MethodDeclarations
  syntax/java/types/ParameterizedTypes
  syntax/java/names/Names
  syntax/java/interfaces/Annotations
  syntax/java/expressions/Main
  syntax/java/statements/Blocks

context-free sorts

  ConstructorDeclaration
  ConstructorModifier
  ConstructorInvocation

context-free syntax
  
  ConstructorDeclaration.ConstrDecl = <
  <{ConstructorModifier " "}*> <TypeParameters?> <Id>(<FormalParams>) <Throws?>{
    <ConstructorInvocation?>
    <{BlockStatement "\n"}*>
  }>
  
  ConstructorModifier = Annotation
  ConstructorModifier = Public
  ConstructorModifier = Private
  ConstructorModifier = Protected
  
  ConstructorInvocation.AltConstrInv      = <<TypeArguments?>this(<{Expression ", "}*>);>
  ConstructorInvocation.SuperConstrInv    = <<TypeArguments?>super(<{Expression ", "}*>);>
  ConstructorInvocation.ExprNameConstrInv = <<Expression>.<TypeArguments?>super(<{Expression ", "}*>);>