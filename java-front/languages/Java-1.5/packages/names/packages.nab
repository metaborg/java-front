module languages/Java-1.5/packages/names/packages

imports

	languages/Java-1.5/classes/names/classes
	languages/Java-1.5/types/names/types
	languages/Java-1.5/packages/trans/desugar
  include/Java

namespaces

  Package
  DefaultPackage
  CompilationUnit
  
properties

	pkgannotations of Package : List(Anno)

binding rules // Compilation unit

	File(_):
		scopes CompilationUnit

	CompilationUnit(name, _, _):
		defines CompilationUnit name
		scopes Type, Method, Field

binding rules // Package

	DefaultPackageDec(p, _):
		defines non-unique DefaultPackage p
		non-transitively scopes Package, Type

	PackageDec(p, _):
		defines non-unique Package p
		non-transitively scopes Package, Type
	
  PackageDec(a*, p, _):
    defines non-unique Package p
    	of pkgannotations a*
    non-transitively scopes Package, Type

binding rules // Imports

  TypeImportDec(i):
    imports
    	Type i
    
  TypeImportOnDemandDec(i):
    imports 
    	Type 
    	from Package i 
    otherwise
    	imports 
	    	Type 
	    	from Type i 
  
  // TODO: modifiers is a list.
  StaticImportDec(t, s):
    imports 
    	Method s 
    		of modifiers Static() 
  		from Type t 
    otherwise
    	imports
    		Field s 
    			of modifiers Static() 
  			from Type t
  
  // TODO: modifiers is a list.
  StaticImportOnDemandDec(t):
    imports 
      Method, Field 
        of modifiers Static()
      from Type t
