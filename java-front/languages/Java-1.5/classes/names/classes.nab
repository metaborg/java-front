module languages/Java-1.5/classes/names/classes

imports

  lib/nabl/-
  lib/task/-
  lib/properties/-
  lib/relations/-
  lib/types/-
  lib/editor/-
  
  include/Java
  languages/Java-1.5/classes/trans/desugar
	languages/Java-1.5/types/names/types
	languages/Java-1.5/types/types/references.generated
  types
  varids

namespaces

  Constructor
  Field
  ImplicitField
  Method 
  Variable
  Label
  
properties

	kind            of Type        : TypeKind
  access          of Type        : Access
  context         of Type        : Context
  modifiers       of Type        : List(ClassMod)
  access          of Field       : Access
  context         of Field       : Context
  modifiers       of Field       : List(FieldMod)
  access          of Method      : Access
  context         of Method      : Context
  modifiers       of Method      : List(MethodMod)
  parameter-types of Method      : List(Type)
  throws          of Method      : Throws
  access          of Constructor : Access
  context         of Constructor : Context
  modifiers       of Constructor : List(ConstrMod)
  modifiers       of Variable    : List(VarMod)
  type-parameters of Type        : TypeParams
  type-parameters of Method      : TypeParams
  var-id          of Variable    : INT

binding rules // Types 

  ClassDec(acc, ctx, mod*, c, tp*, None(), _, body):
    defines Type c
      of access acc
      of context ctx
      of modifiers mod*
      of type-parameters tp*
      of kind Class()
      of type RefType(TypeName(c), None())
    implicitly defines ImplicitField "this" 
    	of type RefType(TypeName(c), None())
    scopes Type, TypeParam, Field, ImplicitField, Method, Constructor
    
  ClassDec(acc, ctx, mod*, c, tp*, SuperDec(sc), _, body):
    defines Type c
      of access acc
      of context ctx
      of modifiers mod*
      of type-parameters tp*
      of kind Class()
      of type RefType(TypeName(c), None())
    imports
      Field, imported Field, 
      Method, imported Method
      from Type sc
    implicitly defines ImplicitField "this" 
    	of type RefType(TypeName(c), None())
    implicitly defines ImplicitField "super" 
    	of type st
    	where sc has type st
    scopes Type, TypeParam, Field, ImplicitField, Method, Constructor

  ImplementsDec(_, it):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type it
  
binding rules // Fields

	// TODO: fields are subsequently scoped, but only inside the class.
  FieldDec(acc, ctx, mod*, t, VarDec(f)):
    defines Field f 
      of access acc
      of context ctx
      of modifiers mod*
      of type t'
      where t has type t'
      // in subsequent scope
  
  // TODO: fields are subsequently scoped, but only inside the class.
  FieldDec(acc, ctx, mod*, t, VarDec(f, _)):
    defines Field f 
      of access acc
      of context ctx
      of modifiers mod*
      of type t'
      where t has type t'
      // in subsequent scope 
      
binding rules // Methods

	// TODO: imports fields into body
  MethodDec(acc, ctx, mod*, tp*, t, m, p*, th, body):
    defines Method m
      of access acc
      of context ctx
      of modifiers mod*
      of type-parameters tp*
      of type t'
      of parameter-types pt*
      of throws th
      where p* has type pt*
      and t has type t'
    // imports Field from enclosing Type into body
    scopes Variable, Label
    
  Param(mod*, t, p, i):
    defines Variable p
      of modifiers mod*
      of type t'
      of var-id i
      where t has type t'

  VarArityParam(mod*, t, p, i):
    defines Variable p
      of modifiers mod*
      of type t'
      of var-id i
      where t has type t'
      
binding rules // Constructors

  // TODO: implicitly define: this, super. These have a different meaning than the class ones.
  ConstrDec(acc, mod*, tp*, c, p*, th, body):
    defines Constructor c
      of access acc
      of modifiers mod*
      of type-parameters tp*
      of throws th
      of context Instance()
      of parameter-types pt*
      of type t
      where p* has type pt*
      and c refers to Type t
    scopes Variable


binding rules // Enums

  EnumDec(EnumDecHead(m*, e, _), _):
    defines Type e 
      of modifiers m*
    scopes Field
  
  EnumConst(e, _, _):
    defines Field e 
