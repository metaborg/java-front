%%%
 %% Section 8.1: Class Declarations
 %%%
module ClassDeclarations

imports 

	Identifiers	
	Modifiers	
	ReferenceTypes	
	TypeVariables	
	FieldDeclarations	
	MethodDeclarations	
	InstanceInitializers	
	StaticInitializers	
	ConstructorDeclarations	
	EnumDeclarations	
	InterfaceDeclarations
	Annotations

sorts

  ClassDec ClassBody ClassDecHead

context-free syntax

  ClassDec = EnumDec 
  ClassDec.ClassDec = <<ClassDecHead> <ClassBody>> 
  ClassBody.ClassBody = <{ <ClassBodyDec*> }> 
  ClassDecHead.ClassDecHead = <<AnnoOrClasMod*> class <Id> <TypeParams?> <Super?> <Interfaces?>> 
  AnnoOrClasMod = Anno 
  AnnoOrClasMod = ClassMod 

sorts

  ClassMod

context-free syntax

  ClassMod = Abstract 
  ClassMod = Public 
  ClassMod = Protected 
  ClassMod = Private 
  ClassMod = Static 
  ClassMod = Final 
  ClassMod = StrictFP 

sorts

  Super

context-free syntax

  Super.SuperDec = <extends <ClassType>> 

sorts

  Interfaces

context-free syntax

  Interfaces.ImplementsDec = <implements <{InterfaceType ","}+>> 

sorts

  ClassBodyDec ClassMemberDec

context-free syntax

  ClassBodyDec = ClassMemberDec 
  ClassBodyDec = InstanceInit 
  ClassBodyDec = StaticInit 
  ClassBodyDec = ConstrDec 

context-free syntax

  ClassMemberDec = FieldDec 
  ClassMemberDec = MethodDec 
  ClassMemberDec = ClassDec 
  ClassMemberDec = InterfaceDec 
  ClassMemberDec.Semicolon = <;> 
