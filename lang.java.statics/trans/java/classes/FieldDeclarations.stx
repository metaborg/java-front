module java/classes/FieldDeclarations

imports

  signatures/java/classes/FieldDeclarations-sig

  java/arrays/ArrayInitializers
  java/classes/Main
  java/expressions/Main
  java/interfaces/Annotations
  java/names/ExpressionNames
  java/names/TypeNames
  java/names/PackageOrTypeNames
  java/types/Main
  java/types/Subtyping
  java/types/ParameterizedTypes
  java/types/PrimitiveTypes
  java/types/ReferenceTypes


rules // 8.3. Field Declarations

  fldDeclOk : scope * FieldDeclaration

  fldDeclOk(s, FieldDecl(fldModList, unannType, varDeclList)) :-
  {T}
    fldModListOk(s, fldModList),
    T == unannTypeOk(s, unannType),
    varDeclListOk(s, T, varDeclList).


  varDeclIdOk : scope * TYPE * VarDeclId * scope

  varDeclIdOk(s, T, VariableDecl(id), s_def) :-
    declareExprName(s_def, id, T).

  varDeclIdOk(s, T, VariableDeclArray(id, annDimList), s_def) :-
    annDimListOk(s, annDimList),
    declareExprName(s_def, id, _). // FIXME Make array type


  varDeclOk : scope * TYPE * VarDecl
  varDeclListOk maps varDeclOk(*, *, list(*))

  varDeclOk(s, T, VariableDeclInit(varDeclId, varInit)) :-
    varDeclIdOk(s, T, varDeclId, s),
    varInitOk(s, T, varInit).

  varDeclOk(s, T, VarDeclId2VarDecl(varDeclId)) :-
    varDeclIdOk(s, T, varDeclId, s).


  varInitOk : scope * TYPE * VariableInitializer
  varInitListOk maps varInitOk(*, *, list(*))

  varInitOk(s, T, Expression2VariableInitializer(expr)) :-
  {T'}
    T' == exprOk(s, expr),
    subType(T', T).

  varInitOk(s, T, ArrayInitializer2VariableInitializer(arrayInit)) :-
    arrayInitOk(s, T, arrayInit).


  fldModOk : scope * FieldModifier
  fldModListOk maps fldModOk(*, list(*))

  fldModOk(s, Annotation2FieldModifier(anno)) :-
    annoOk(s, anno),
    true.
  fldModOk(s, Public2FieldModifier(_)) :- true.
  fldModOk(s, Protected2FieldModifier(_)) :- true.
  fldModOk(s, Private2FieldModifier(_)) :- true.
  fldModOk(s, Static2FieldModifier(_)) :- true.
  fldModOk(s, Final2FieldModifier(_)) :- true.
  fldModOk(s, Transient2FieldModifier(_)) :- true.
  fldModOk(s, Volatile2FieldModifier(_)) :- true.


  unannTypeOk : scope * UnannType -> TYPE

  unannTypeOk(s, UnannPrimitiveType2UnannType(unannPrimType)) = unannPrimTypeOk(unannPrimType).

  unannTypeOk(s, UnannReferenceType2UnannType(unannRefType)) = unannRefTypeOk(s, unannRefType).


  unannPrimTypeOk : UnannPrimitiveType -> TYPE

  unannPrimTypeOk(NumericType(numType)) = numTypeOk(numType).

  unannPrimTypeOk(BooleanType()) = BOOLEAN().


  unannRefTypeOk : scope * UnannReferenceType -> TYPE

  unannRefTypeOk(s, UnannClassType2UnannReferenceType(unannClsType)) = unannClsTypeOk(s, unannClsType).

  unannRefTypeOk(s, UnannArrayType2UnannReferenceType(unannArrayType)) = unannArrayTypeOk(s, unannArrayType).


  unannClsTypeOk : scope * UnannClassType -> TYPE

  unannClsTypeOk(s, ClassType(id, typeArgsOpt)) = typeDeclType(typeDecl) :-
    typeDecl == singleTypeDecl(id, resolveLexicalTypeNames(s, id)),
    _ == typeArgsOptOk(s, typeArgsOpt),
    true.

  unannClsTypeOk(s, UnannClassOrInterfaceTypeMember(unannClsType, annoList, id, typeArgsOpt)) = typeDeclType(typeDecl) :-
  {s_pkgOrCls}
    s_pkgOrCls == unannPkgOrClsTypeOk(s, unannClsType),
    annoListOk(s, annoList),
    typeDecl == singleTypeDecl(id, resolveMemberTypeNames(s_pkgOrCls, id)),
    _ == typeArgsOptOk(s, typeArgsOpt),
    true.


  unannPkgOrClsTypeOk : scope * UnannClassType -> scope

  unannPkgOrClsTypeOk(s, ClassType(id, typeArgsOpt)) = s_pkgOrCls :-
  {pkgOrTypeDecl}
    pkgOrTypeDecl == resolveLexicalPkgOrTypeName(s, id),
    s_pkgOrCls == pkgOrTypeDeclScope(pkgOrTypeDecl),
    _ == typeArgsOptOk(s, typeArgsOpt),
    true.

  unannPkgOrClsTypeOk(s, UnannClassOrInterfaceTypeMember(unannClsType, annoList, id, typeArgsOpt)) = s_pkgOrCls :-
  {pkgOrTypeDecl s_pkgOrCls'}
    s_pkgOrCls' == unannPkgOrClsTypeOk(s, unannClsType),
    annoListOk(s, annoList),
    pkgOrTypeDecl == resolveMemberPkgOrTypeName(s_pkgOrCls', id),
    s_pkgOrCls == pkgOrTypeDeclScope(pkgOrTypeDecl),
    _ == typeArgsOptOk(s, typeArgsOpt),
    true.


  unannArrayTypeOk : scope * UnannArrayType -> TYPE

  unannArrayTypeOk(s, UnannArrayTypePrimitive(unannPrimType, annDimList)) = _ :-
  {T}
    T == unannPrimTypeOk(unannPrimType),
    annDimListOk(s, annDimList),
    true.

  unannArrayTypeOk(s, UnannArrayTypeClassType(unannClsType, annDimList)) = _ :-
  {T}
    T == unannClsTypeOk(s, unannClsType),
    annDimListOk(s, annDimList),
    true.


