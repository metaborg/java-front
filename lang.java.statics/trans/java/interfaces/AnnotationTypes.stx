module java/interfaces/AnnotationTypes

imports

  signatures/java/interfaces/AnnotationTypes-sig

  java/classes/ClassDeclarations
  java/classes/FieldDeclarations
  java/interfaces/Annotations
  java/interfaces/InterfaceDeclarations
  java/interfaces/ConstantDeclarations
  java/interfaces/Main
  java/names/Main
  java/names/TypeNames
  java/types/ReferenceTypes


rules // 9.6. Annotation Types

  annoTypeDeclOk : scope * scope * scope * AnnotationTypeDeclaration * scope * scope

  annoTypeDeclOk(s, s_anno, s_annoStatic, AnnoDec(intfModList, id, annoTypeMemDeclList), s_def, s_staticDef) :-
  {isStatic}
    intfModListOk(s, intfModList),
    isStatic == hasStaticIntfMod(intfModList),

    addLexical(s_anno, isStatic, s),

    declareType2(s_def, s_staticDef, isStatic, id, s_anno, INTF()),

    // FIXME Object as supertype?

    s_anno -STATIC_MEMBERS-> s_annoStatic,

    annoTypeMemDeclListOk(s_anno, annoTypeMemDeclList, s_anno, s_annoStatic).


  annoTypeMemDeclOk : scope * AnnotationTypeMemberDeclaration * scope * scope
  annoTypeMemDeclListOk maps annoTypeMemDeclOk(*, list(*), *, *)

  annoTypeMemDeclOk(s, AnnotationTypeElementDeclaration2AnnotationTypeMemberDeclaration(annoTypeElemDecl), s_def, s_staticDef) :-
    annoTypeElemDeclOk(s, annoTypeElemDecl),
    true.

  annoTypeMemDeclOk(s, ConstantDeclaration2AnnotationTypeMemberDeclaration(constDecl), s_def, s_staticDef) :-
    constDeclOk(s, constDecl, s_staticDef),
    true.

  annoTypeMemDeclOk(s, ClassDeclaration2AnnotationTypeMemberDeclaration(clsDecl), s_def, s_staticDef) :-
  {s_cls s_clsStatic}
    new s_cls s_clsStatic,
    clsDeclOk(s, s_cls, s_clsStatic, addImplicitClassMemMods(clsDecl), s_def, s_staticDef).

  annoTypeMemDeclOk(s, InterfaceDeclaration2AnnotationTypeMemberDeclaration(intfDecl), s_def, s_staticDef) :-
  {s_intf s_intfStatic}
    new s_intf s_intfStatic,
    intfDeclOk(s, s_intf, s_intfStatic, addImplicitIntfMemMods(intfDecl), s_def, s_staticDef).

  annoTypeMemDeclOk(s, SemiColon2AnnotationTypeMemberDeclaration(_), s_def, s_staticDef) :-
    true.


  annoTypeElemDeclOk : scope * AnnotationTypeElementDeclaration

  annoTypeElemDeclOk(s, AnnotationTypeElementDecl(annoTypeElemModList, unannType, id, annDimsEmpty, defaultValOpt)) :-
  {T U}
    annoTypeElemModListOk(s, annoTypeElemModList),
    T == unannTypeOk(s, unannType),
    U == annDimsEmptyOk(s, T, annDimsEmpty),
    defaultValOptOk(s, defaultValOpt).


  annoTypeElemModOk : scope * AnnotationTypeElementModifier
  annoTypeElemModListOk maps annoTypeElemModOk(*, list(*))

  annoTypeElemModOk(s, Annotation2AnnotationTypeElementModifier(anno)) :-
    annoOk(s, anno),
    true.
  annoTypeElemModOk(s, Public2AnnotationTypeElementModifier(_)) :- true.
  annoTypeElemModOk(s, Abstract2AnnotationTypeElementModifier(_)) :- true.


  defaultValOk : scope * DefaultValue
  defaultValOptOk maps defaultValOk(*, list(*))

  defaultValOk(s, DefaultVal(elemVal)) :-
    elemValOk(s, elemVal),
    true.


